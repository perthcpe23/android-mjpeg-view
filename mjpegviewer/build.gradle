apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'signing'

android {
    namespace 'com.longdo.mjpegviewer'
    compileSdkVersion 33
    buildToolsVersion '30.0.3'

    defaultConfig {
        aarMetadata {
            minCompileSdk = 14
        }
        minSdkVersion 19
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    publishing {
        singleVariant('release') {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId 'com.perthcpe23.dev'
                artifactId 'android-mjpeg-view'
                version '1.1.2'

                pom {
                    name = "Android Mjpeg View"
                    description = "Android custom View to render Mjpeg stream"
                    url = "https://github.com/perthcpe23/android-mjpeg-view"
                    licenses {
                        license {
                            name = "The MIT License (MIT)"
                            url = "https://opensource.org/license/mit/"
                        }
                    }
                    developers {
                        developer {
                            id = "perthcpe23"
                            name = "Punyapat S"
                            email = "perth.s28@gmail.com"
                        }
                    }
                    scm {
                        connection = "scm:git:git://github.com/perthcpe23/android-mjpeg-view.git"
                        developerConnection = "scm:git:ssh://github.com:perthcpe23/android-mjpeg-view.git"
                        url = "http://github.com/perthcpe23/android-mjpeg-view/tree/master"
                    }
                }
            }
        }
        repositories {
            maven {
                name = "Central" //  optional target repository name
                url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
                credentials {
                    username = System.getenv("MAVEN_USERNAME")
                    password = System.getenv("MAVEN_PASSWORD")
                }
            }
            maven {
                name = "Local" //  optional target repository name
                url = layout.buildDirectory.dir('local_repo')
            }
        }
    }
}

signing {
    sign publishing.publications
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.6.1'
}